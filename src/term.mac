ROUTINE term
term() public {
  set pad = 20
  for i=1:1 {
    set line = $text(lines+i)
    quit:line=""
    set $listbuild(, left, right) = $listfromstring(line, ";")
    set right = $zconvert(right, "I", "JS")
    do write(pad, left, right)
  }
  do write(pad, "16 color -", $$colors16())
  do write(pad, "256 color ┬", $$colors256(0))
  do write(pad, "          │", $$colors256(1))
  do write(pad, "          │", $$colors256(2))
  do write(pad, "          │", $$colors256(3))
  do write(pad, "          │", $$colors256(4))
  do write(pad, "          │", $$colors256(5))
  do write(pad, "          └", $$colors256(6))
  do write(pad, "True color ┬", $$colorsTrue(1, 0))
  do write(pad, "       Red │", $$colorsTrue(1, 1))
  do write(pad, "           │", $$colorsTrue(1, 2))
  do write(pad, "           └", $$colorsTrue(1, 3))
  do write(pad, "     Green ┌", $$colorsTrue(2, 0))
  do write(pad, "           │", $$colorsTrue(2, 1))
  do write(pad, "           │", $$colorsTrue(2, 2))
  do write(pad, "           └", $$colorsTrue(2, 3))
  do write(pad, "      Blue ┌", $$colorsTrue(3, 0))
  do write(pad, "           │", $$colorsTrue(3, 1))
  do write(pad, "           │", $$colorsTrue(3, 2))
  do write(pad, "           └", $$colorsTrue(3, 3))
}
write(pad, left, right) {
  write !,$justify(left, pad), right
  write *27, "[0m"
}
colors16() public {
  set colors = ""
  for i=0:1:7 {
    set colors = colors _ $char(27) _ "[4" _ i _ "m "
  }
  for i=0:1:7 {
    set colors = colors _ $char(27) _ "[1;4" _ i _ "m "
  }
  quit colors
}
colors256(part, chunk = 36) public {
  set colors = ""
  set from = 16 + (part * chunk)
  set to = from + chunk - 1
  for i=from:1:to {
    set colors = colors _ $char(27) _ "[48;5;" _ i _ "m "
    quit:i=255
  }
  quit colors
}
colorsTrue(pos = 1, part, chunk = 64) public {
  set colors = ""
  set from = part * chunk
  set to = from + chunk - 1
  for i=from:1:to {
    set color = "0;0;0"
    set $piece(color, ";", pos) = i
    set colors = colors _ $char(27) _ "[48;2;" _ color _ "m "
    quit:i=255
  }
  quit colors
}
lines
 ;  Ascii ─; abc123
 ;  CJK ─; 汉语, 漢語, 日本語, 한국어
 ;  Powerline ─; \ue0b2\ue0b0\ue0b3\ue0b1\ue0b6\ue0b4\ue0b7\ue0b5\ue0ba\ue0b8\ue0bd\ue0b9\ue0be\ue0bc
 ;  Box drawing ┬; ┌─┬─┐ ┏━┳━┓ ╔═╦═╗ ┌─┲━┓ ╲   ╱
 ;              │; │ │ │ ┃ ┃ ┃ ║ ║ ║ │ ┃ ┃  ╲ ╱
 ;              │; ├─┼─┤ ┣━╋━┫ ╠═╬═╣ ├─╄━┩   ╳
 ;              │; │ │ │ ┃ ┃ ┃ ║ ║ ║ │ │ │  ╱ ╲
 ;              └; └─┴─┘ ┗━┻━┛ ╚═╩═╝ └─┴─┘ ╱   ╲
 ;  Block elem ─; ░▒▓█ ▁▂▃▄▅▆▇█ ▏▎▍▌▋▊▉
 ;  Emoji ─; 😉👋
 ; Styles ─; \x1b[1mBold\x1b[0m, \x1b[2mFaint\x1b[0m, \x1b[3mItalics\x1b[0m, \x1b[7mInverse\x1b[0m, \x1b[9mStrikethrough\x1b[0m, \x1b[8mInvisible\x1b[0m
 ; Underlines ─; \x1b[4:1mStraight\x1b[0m, \x1b[4:2mDouble\x1b[0m, \x1b[4:3mCurly\x1b[0m, \x1b[4:4mDotted\x1b[0m, \x1b[4:5mDashed\x1b[0m
 ;;